class Tetris {
    field Array board;
    field int columnCount;
    field int rowCount;
    field Piece falling;
    field boolean gameRunning;
    field int gameTime;

    static int KEYLEFT;
    static int KEYRIGHT;
    static int KEYSPACE;
    static int KEYDOWN;

    function void init() {
        let KEYLEFT = 130;
        let KEYRIGHT = 132;
        let KEYSPACE = 32;
        let KEYDOWN = 133;
        return;
    }

    constructor Tetris new() {
        var int i, j;
        var Array row;
        let columnCount = 10;
        let rowCount = 22;
        let board = Array.new(rowCount);

        let i = 0;
        while (i < rowCount) {
            let row = Array.new(columnCount);
            let board[i] = row;
            while (j < columnCount) {
                let row[j] = false;
                let j = j + 1;
            }
            let i = i + 1;
        }

        do drawBoard();
        return this;
    }

    method void dispose() {
        var int i, j;
        var Array row;
        let i = 0;
        while (i < rowCount) {
            let row = board[i];
            do row.dispose();
            let i = i + 1;
        }
        return;
    }

    method void drawBoard() {
        do Screen.drawLine(192, 0, 320, 0);
        do Screen.drawLine(192, 0, 192, 255);
        do Screen.drawLine(320, 0, 320, 255);
        do Screen.drawLine(192, 255, 320, 255);
        return;
    }

    method void run() {
        var int key, prevKey;

        let falling = getRandomPiece();
        let gameRunning = true;
        let gameTime = 0;
        let prevKey = 0;
        while (gameRunning) {
            let key = Keyboard.keyPressed();
            if (~(key = prevKey)) {
                if (key = KEYLEFT) {
                    do falling.moveLeft();
                    if (collides()) {
                        do falling.moveRight();
                    }
                }
                if (key = KEYRIGHT) {
                    do falling.moveRight();
                    if (collides()) {
                        do falling.moveLeft();
                    }
                }
                if (key = KEYSPACE) {
                    do falling.rotate();
                    if (collides()) {
                        do falling.rotateBack();
                    }
                }
                if (key = KEYDOWN) {
                    do falling.moveDown();
                    if (collides()) {
                        do handleDownwardCollision();
                    }
                }
            }
            if (Util.mod(gameTime, 100) = 0) {
                do falling.moveDown();
                if (collides()) {
                    do handleDownwardCollision();
                }
            }

            let gameTime = gameTime + 1;
            let prevKey = key;
        }
        return;
    }

    method boolean collides() {
        var Stack positions; 
        var Position pos;

        let positions = falling.computePositions();
        let pos = positions.pop();
        while (pos) {
            if (occupied(pos)) {
                do positions.dispose();
                return true;
            }
            do pos.dispose();
            let pos = positions.pop();
        }
        do positions.dispose();

        return false;
    }

    method boolean occupied(Position pos) {
        var Array row;
        let row = board[pos.r()];
        return row[pos.c()];
    }

    method void handleDownwardCollision() {
        do falling.moveUp();
        do freeze();
        do falling.dispose();
        do wipeConnectedRows();
        if (topRowOccupied()) {
            let gameRunning = false;
        } else {
            let falling = getRandomPiece();
        }
        return;
    }

    method void wipeConnectedRows() {
        var int r;
        let r = 0;
        while (r < 22) {
            if (isConnected(r)) {
                do wipeRow(r);
            } else {
                let r = r + 1;
            }
        }
        return;
    }

    method boolean isConnected(int r) {
        var int c;
        var Array row;
        let c = 0;
        let row = board[r];
        while (c < 10) {
            if (row[c] = false) {
                return false;
            }
            let c = c + 1;
        }
        return true;
    }

    method void wipeRow(int r) {
        var Array thisRow;
        var Array nextRow;
        var int c;

        while (r < 21) {
            let thisRow = board[r];
            let nextRow = board[r+1];
            let c = 0;

            while (c < 10) {
                let thisRow[c] = nextRow[c];
                let c = c + 1;
            }
            let r = r + 1;
        }

        let c = 0;
        while (c < 10) {
            let nextRow[c] = false;
            let c = c + 1;
        }
        return;
    }

    method Piece getRandomPiece() {
        return Piece.new(Random.getNumber(0, 6));
    }

    method void freeze() {
        // TODO
        return;
    }

    method boolean topRowOccupied() {
        // TODO
        return false;
    }
}
