// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.

/**
 * Implements the SpeedTyping game.
 */
class SpeedTypingGame {

    field String keyword;
    field int wordsCount;
    field Array words;
    field String BLANK;

    constructor SpeedTypingGame new() {
        let keyword = String.new(25);
        let BLANK = "                         ";
        let wordsCount = 2;
        let words = Array.new(wordsCount);
        let words[0] = Word.new("ABC", 5, 1, 0);
        let words[1] = Word.new("DEFG", 7, 2, 30);

        return this;
    }

    method void dispose() {
        var int i;
        var Word word;
        let i = 0;
        while (i < wordsCount) {
            let word = words[i];
            do word.dispose();
            let i = i + 1;
        }

        do words.dispose();
        do keyword.dispose();
        do BLANK.dispose();
        do Memory.deAlloc(this);
        return;
    }

    method void run() {
        var char key;
        var boolean exit;

        let exit = false;
        // keycodes textbook pg 71
        while (~exit) {
            while (key = 0) {
                let key = Keyboard.keyPressed();
                do moveWord();
            }

            if (key = 140) { // esc
                let exit = true;
            }
            if (~(key = 140)) {
                if ((key = 129) & (~(keyword.length() = 0))) { // backspace
                    do keyword.eraseLastChar();
                }
                if (~(key = 129) & (~(keyword.length() = 25))) {
                    do keyword.appendChar(key);
                }
                do clearKeyword();
                do Output.moveCursor(20, 0);
                do Output.printString(keyword);
                do matchKeyword();
            }

            while (~(key = 0)) {
                let key = Keyboard.keyPressed();
                do moveWord();
            }
        }
            
        return;
	}

    method void matchKeyword() {
        var int i;
        var Word word;
        let i = 0;
        while (i < wordsCount) {
            let word = words[i];
            if (word.matchKeyword(keyword)) {
                do resetKeyword();
            }
            let i = i + 1;
        }
        return;
    }

    method void resetKeyword() {
        while (keyword.length() > 0) {
            do keyword.eraseLastChar();
        }
        do clearKeyword();
        return;
    }

    method void clearKeyword() {
        do Output.moveCursor(20, 0);
        do Output.printString(BLANK);
        return;
    }

    method void moveWord() {
        var int i;
        var Word word;
        let i = 0;
        while (i < wordsCount) {
            let word = words[i];
            do word.animate();
            let i = i + 1;
        }
        do Sys.wait(5);
        return;
    }
}
