// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.

/**
 * Implements the SpeedTyping game.
 */
class SpeedTypingGame {

    field String keyword;
    field Word word;
    field Word word2;
    field String BLANK;

    constructor SpeedTypingGame new() {
        let keyword = String.new(25);
        let BLANK = "                         ";
        let word = Word.new("ABC", 5, 1, 0);
        let word2 = Word.new("DEFG", 7, 2, 30);

        return this;
    }

    method void dispose() {
        do keyword.dispose();
        do word.dispose();
        do word2.dispose();
        do Memory.deAlloc(this);
        return;
    }

    method void run() {
        var char key;
        var boolean exit;

        let exit = false;
        // keycodes textbook pg 71
        while (~exit) {
            while (key = 0) {
                let key = Keyboard.keyPressed();
                do moveWord();
            }

            if (key = 140) { // esc
                let exit = true;
            }
            if (~(key = 140)) {
                if ((key = 129) & (~(keyword.length() = 0))) { // backspace
                    do keyword.eraseLastChar();
                }
                if (~(key = 129) & (~(keyword.length() = 25))) {
                    do keyword.appendChar(key);
                }
                do clearKeyword();
                do Output.moveCursor(20, 0);
                do Output.printString(keyword);
                if (word.matchKeyword(keyword)) {
                    do resetKeyword();
                }
                if (word2.matchKeyword(keyword)) {
                    do resetKeyword();
                }
            }

            while (~(key = 0)) {
                let key = Keyboard.keyPressed();
                do moveWord();
            }
        }
            
        return;
	}

    method void resetKeyword() {
        while (keyword.length() > 0) {
            do keyword.eraseLastChar();
        }
        do clearKeyword();
        return;
    }

    method void clearKeyword() {
        do Output.moveCursor(20, 0);
        do Output.printString(BLANK);
        return;
    }

    method void moveWord() {
        do word.animate();
        do word2.animate();
        do Sys.wait(5);
        return;
    }
}
