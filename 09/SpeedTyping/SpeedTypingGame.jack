// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.

/**
 * Implements the SpeedTyping game.
 */
class SpeedTypingGame {

    field String keyword;
    field Word word;

    constructor SpeedTypingGame new() {
        let keyword = String.new(25);
        let word = Word.new("ABC", 5, 1);

        return this;
    }

    method void dispose() {
        do keyword.dispose();
        do word.dispose();
        do Memory.deAlloc(this);
        return;
    }

    method void run() {
        var char key;
        var boolean exit;

        let exit = false;
        // keycodes textbook pg 71
        while (~exit) {
            while (key = 0) {
                let key = Keyboard.keyPressed();
                do moveWord();
            }

            if (key = 140) { // esc
                let exit = true;
            }
            if (~(key = 140)) {
                if ((key = 129) & (~(keyword.length() = 0))) { // backspace
                    do keyword.eraseLastChar();
                }
                if (~(key = 129) & (~(keyword.length() = 25))) {
                    do keyword.appendChar(key);
                }
                do clearKeyword();
                do Output.moveCursor(20, 0);
                do Output.printString(keyword);
                do word.matchKeyword(keyword);
            }

            while (~(key = 0)) {
                let key = Keyboard.keyPressed();
                do moveWord();
            }
        }
            
        return;
	}

    method void clearKeyword() {
        do Output.moveCursor(20, 0);
        do Output.printString("                         ");
        return;
    }

    method void moveWord() {
        do word.animate();
        do Sys.wait(5);
        return;
    }
}
